import os
import glob

# Load configuration
configfile: "config.yaml"

# Helper to get sample names from the assembly directory
ASSEMBLY_DIR = config["assemblies_dir"]
EXT = config["extension"]
SAMPLES = [os.path.basename(f).replace(EXT, "") for f in glob.glob(f"{ASSEMBLY_DIR}/*{EXT}")]
REF_GENOME = config["reference_genome"]

for key, value in config["outdir"].items():
    os.makedirs(value, exist_ok=True)

rule all:
    input:
        node_features=expand(os.path.join(config["outdir"]["node_features"], "{sample}.csv"), sample=SAMPLES),
        snps_adj_matrix=os.path.join(config["outdir"]["edge_matrices"], "snps_adj_matrix.csv"),
        fcgr_adj_matrix=os.path.join(config["outdir"]["edge_matrices"], "fcgr_adj_matrix.csv")

# rule all:
#     input:
#         # 1. Node feature vectors for each assembly
#         node_features = expand(os.path.join(config["outdir"]["node_features"], "{sample}.pt"), sample=SAMPLES),
#         vcf_files = expand(os.path.join(config["outdir"]["variants"], "{sample}/vcf.txt"), sample=SAMPLES),
#         # 2. Adjacency matrices
#         os.path.join(config["outdir"]["edge_matrices"], "snps_adj_matrix.csv"),
#         os.path.join(config["outdir"]["edge_matrices"], "fcgr_adj_matrix.csv"),

#         # 3. Clean up middle directories
#         clean_up_dirs = [config["outdir"]["extracted_unitigs"], config["outdir"]["variants"]]
#     shell:
#         "rm -rf {input.clean_up_dirs}"

# Rules to extract unitigs from assemblies
rule prepare_strain_list:
    output:
        "strain_list.txt"
    run:
        files = glob.glob(os.path.join(ASSEMBLY_DIR, "*" + EXT))
        
        with open(output[0], "w") as out:
            for f in files:
                abs_path = os.path.abspath(f)
                out.write(abs_path + "\n")

rule query_unitigs:
    input:
        strain_list="strain_list.txt",
        unitig_list=config["unitig_list"]
    output:
        "unitig_caller.pyseer"
    threads: 4
    shell:
        """
        unitig-caller --simple --refs {input.strain_list} --unitigs {input.unitig_list}
        """

rule convert_unitig_to_node_features:
    input:
        unitig_query_results="unitig_caller.pyseer",
        unitig_list=config["unitig_list"]
    params:
        isolate_ids=",".join(SAMPLES),
        outdir=config["outdir"]["node_features"]
    output:
        expand(os.path.join(config["outdir"]["node_features"], "{sample}.csv"), sample=SAMPLES)
    threads: 4
    shell:
        """
        python convert_unitig_to_node_features.py --unitig_query_results {input.unitig_query_results} \
                                                  --unitig_list {input.unitig_list} \
                                                  --isolate_ids {params.isolate_ids} \
                                                  --outdir {params.outdir}
        """

# Rules to call variants and compute SNP/FCGR-based adjacency matrices
rule call_variants:
    input:
        assembly = ASSEMBLY_DIR + "/{isolate_id}" + EXT
    params:
        ref_genome = REF_GENOME
    output:
        outdir=directory(os.path.join(config["outdir"]["variants"], "{isolate_id}"))
    shell:
        """
        snippy --cpus 2 \
               --outdir {output.outdir} \
               --ref {params.ref_genome} \
               --contigs {input.assembly} \
               --cleanup
        """

rule get_snps_distance_matrix:
    input:
        vcf_results=expand(os.path.join(config["outdir"]["variants"], "{sample}"), sample=SAMPLES)
    params:
        vcf_files=lambda x: ",".join(expand(os.path.join(config["outdir"]["variants"], "{sample}", "snps.tab"), sample=SAMPLES)),
        keep_proportion = config["keep_proportion"]
    output:
        os.path.join(config["outdir"]["edge_matrices"], "snps_adj_matrix.csv")
    shell:
        """
        python compute_snp_distance_matrix.py --vcf_files {params.vcf_files} \
                                              --keep_proportion {params.keep_proportion} \
                                              --out_fp {output}
        """

rule get_fcgr_distance_matrix:
    input:
        vcf_results=expand(os.path.join(config["outdir"]["variants"], "{sample}"), sample=SAMPLES)
    params:
        aligned_assembly_files = lambda x: ",".join(expand(os.path.join(config["outdir"]["variants"], "{sample}", "snps.aligned.fa"), sample=SAMPLES)),
        position_file = config["fcgr"]["position_fp"],
        kmer_size = config["fcgr"]["kmer_size"],
        contig_id = config["fcgr"]["contig_id"],
        keep_proportion = config["keep_proportion"]
    output:
        os.path.join(config["outdir"]["edge_matrices"], "fcgr_adj_matrix.csv")
    shell:
        """
        python compute_fcgr_distance_matrix.py --aligned_assembly_files {params.aligned_assembly_files} \
                                               --position_file {params.position_file} \
                                               --kmer_size {params.kmer_size} \
                                               --contig_id {params.contig_id} \
                                               --keep_proportion {params.keep_proportion} \
                                               --output {output}
        """



